/* ======================================================================================
   Module:      AssignRelationships
   Description: Generates XML script to update Related Accounts
   Author:      David Hirshfield 
   --------------------------------------------------------------------------------------
   Notes:       
   ====================================================================================== */
 
// the main function that generates the XML Loader file
xmlScript = LAMBDA(dataRows, 
    // check that required input parameters are all present
    IF( --validateParameters , errorMessage(validateParameters), 
        
        // generate the content
        LET(rowCount, dataRowCount,
            xmlContent, VSTACK(
                            xmlHeaderBlock(rowCount), 
                            xmlDataBlock(dataRows),
                            xmlFooterBlock(rowCount)
                        ),
            CHOOSECOLS(SORT(xmlContent), 2)
        )
    )
);

// generates the header block for the XML script
xmlHeaderBlock = LAMBDA(rowCount, 
    VSTACK(
        HSTACK(0, xmlHeaderEncoding(0)),
        HSTACK(0.1, xmlHeaderDescription(rowCount)),
        HSTACK(0.2, xmlHeaderSource(0)),
        HSTACK(0.3, xmlHeaderInfo(0)),
        HSTACK(0.4, xmlBonaireTagOpen(0)),
        HSTACK(0.5, xmlCharComment(0)),
        HSTACK(0.6, xmlHeaderTag(0)),
        HSTACK(0.7, xmlBodyTagOpen(0))
    )
);

// generates the footer block for the XML script
xmlFooterBlock = LAMBDA(rowCount, 
    VSTACK(
        HSTACK(rowCount + 1, xmlBodyTagClose(0)),
        HSTACK(rowCount + 1.1, xmlTrailerTag(rowCount)),
        HSTACK(rowCount + 1.2, xmlBonaireTagClose(0))
    )
);

// generates the data rows for the XML script
xmlDataBlock = LAMBDA(dataRows,
    LET(
        // a sequential list used for sorting
        seqList, MAKEARRAY(dataRowCount, 1, LAMBDA(row, col, row)),
        
        // a two-column array consisting of the sequence number and the set of tags and values for each row  
        HSTACK(seqList, REPT(" ", 6) & BYROW(dataRows, xmlTagValueList))
    )
);

dataRowCount = ROWS(FILTER(accountRelationships, accountRelationships[RelationshipType]<>"", 0));

// ------------------------------------
//   xmlHeaderBlock functions
// ------------------------------------ 
xmlHeaderEncoding = LAMBDA(id, "<?xml version=" & Qt & "1.0" & Qt & " encoding=" & Qt & "ISO-8859-15" & Qt & "?>");
xmlHeaderDescription = LAMBDA(id, "<!-- This file loads " & TEXT(id, "#,##0") & " " & dataType & " to the " & attrClientName & " " & attrProjectPhase & " environment -->");

xmlHeaderSource = LAMBDA(id, "<!-- The data was sourced from the " & 
   attrClientName & " " & attrProjectPhase & " " & 
   IF(attrScope="Single", attrPrimaryEnvironment, attrSecondaryEnvironment) & " environment -->"
);

xmlHeaderInfo = LAMBDA(id,   "<!-- Generated by " & currentUser & " using the " & workbookName & " data loader on " & TEXT(NOW(), dateFormat) & " -->");
xmlBonaireTagOpen = LAMBDA(id, "<Bonaire>");
xmlCharComment = LAMBDA(id, "   <!-- The ConvertSpecialCharacter tag is set to 'false' if any row contains an escaped character -->");
xmlBodyTagOpen = LAMBDA(id, "   <RevportBody DataType=" & Qt & xmlDataType & Qt & ">");
xmlHeaderTag = LAMBDA(id, "   <RevportHeader ConvertSpecialCharacter=" & Qt & IF(COUNTIF(accountRelationships[RelationshipType], "*&amp;*") > 0, "false", "true") & Qt &
                          " FileDate=" & Qt & TEXT(xmlFileDate, dateFormat) & Qt & " InputDateFormat=" & Qt & dateFormat & Qt & " />");

// ------------------------------------
//   xmlFooterBlock functions
// ------------------------------------ 
xmlBodyTagClose = LAMBDA(id, "   </RevportBody>");
xmlTrailerTag = LAMBDA(id,   "   <RevportTrailer DataType=" & Qt & xmlDataType & Qt & " RecordCount=" & Qt & id & Qt & " />");
xmlBonaireTagClose = LAMBDA(id, "</Bonaire>");

// ------------------------------------
//   xmlDataBlock functions
// ------------------------------------ 

// Returns the column value formatted using standard date format
xmlDate = LAMBDA(colValue, 
    TEXT(colValue, dateFormat)
);

// Returns the xmlTag corresponding to the parameter value, found in the header row.
// The source data table is offset from the left by 5 columns
xmlTag = LAMBDA(colValue, INDEX(xmlTagList, COLUMN(colValue) - 5));

// If the value is empty or NULL returns blank. If the tag ends with 'Date' returns the value formatted as a date; otherwise returns the value
xmlValue = LAMBDA(colValue, 
    IFS(// return blank if value is empty or NULL
        OR(ISBLANK(colValue), colValue = "", colValue = "NULL"), "", 
        // convert boolean values to 1 or 0
        colValue = TRUE, 1, 
        colValue = FALSE, 0,
        // non-date column
        RIGHT(xmlTag(colValue), 4) <> "Date", colValue, 
        // date column - default result
        TRUE, xmlDate(colValue) 
      )
);

// Returns the tag for the value, based on the contents of the table header column
xmlTagValue = LAMBDA(colValue,
    LET(xmlVal, xmlValue(colValue),
        // will omit the tag when the value is empty
        IF(xmlVal = "", "",
           xmlTag(colValue) & EqQt & xmlVal & Qt
        )
    )
);

// combines the tag and the value into a single valid XML row
xmlTagValueList = LAMBDA(dataRow, 
    LET(xmlRow, TEXTJOIN(" ", TRUE, MAP(dataRow, xmlTagValue)),
        IF(xmlRow = "", "", // ignore empty rows
            "<" & xmlDataType & " " & TEXTJOIN(" ", TRUE, MAP(dataRow, xmlTagValue) ) & " />" 
        )
    )
);

xmlTagList = accountRelationships[#Headers];
xmlFileDate = '32 - Assign Relationships'!$C$9;

// ------------------------------------
//   Error handling functions
// ------------------------------------ 

// checks parameters and returns a specific error code
validateParameters = IFS(
    // missing header date
    xmlFileDate = 0, 1001,
    // missing start date
    // AND(dateStart = 0, dateEnd <> 0), 1002,
    // // missing end date
    // AND(dateStart <> 0, dateEnd = 0), 1003,
    // // missing start and end date
    // AND(dateStart = 0, dateEnd = 0),  1004,
    //platform = "", 1005,
    // missing data source - does not apply to all data builders
    // dataSourceTag = "", 1006,    
    TRUE, 0
);

// get error message - conditional formatting will use red highlight for messages starting with "Error!"
errorMessage = LAMBDA(errorCode, 
    CHOOSE(errorCode - 1000, 
        "Error! Missing XML Loader Header Date.",
        // "Error! Missing SQL Query Start Date.",
        // "Error! Missing SQL Query End Date.",
        // "Error! Missing both SQL Query Start Date and End Date.",
        // "Error! Missing Source Platform."
        // "Error! Missing Exchange Rate Data Source."
    )
);

Qt = CHAR(34);
EqQt = "=" & Qt;
dateFormat = "MM/dd/yyyy";
dataType = "Account Relationships";
dataTypeDesc = "Assign Account Relationships";
xmlDataType = "RelatedPortfolio";
